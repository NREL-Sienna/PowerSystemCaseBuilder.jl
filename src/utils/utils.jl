function verify_storage_dir(folder::AbstractString = SERIALIZED_DIR)
    directory = abspath(normpath(folder))
    if !isdir(directory)
        mkpath(directory)
    end
end

function check_serialized_storage()
    verify_storage_dir(SERIALIZED_DIR)
    return
end

function clear_serialized_systems(name::String)
    file_names = [name * ext for ext in SERIALIZE_FILE_EXTENSIONS]
    for dir in _get_system_directories(SERIALIZED_DIR)
        for file in file_names
            if isfile(joinpath(dir, file))
                @debug "Deleting file" file
                rm(joinpath(dir, file); force = true)
            end
        end
    end
    return
end

function clear_serialized_system(
    name::String,
    case_args::Dict{Symbol, <:Any} = Dict{Symbol, Any}(),
)
    file_path = get_serialized_filepath(name, case_args)
    if isfile(file_path)
        @debug "Deleting file at " file_path
        rm(file_path; force = true)
    end

    return
end

function clear_all_serialized_systems(path::String)
    for path in _get_system_directories(path)
        rm(path; recursive = true)
    end
end

clear_all_serialized_systems() = clear_all_serialized_systems(SERIALIZED_DIR)
clear_all_serialized_system() = clear_all_serialized_systems()

function get_serialization_dir(case_args::Dict{Symbol, <:Any} = Dict{Symbol, Any}())
    args_string = join(["$key=$value" for (key, value) in case_args], "_")
    hash_value = bytes2hex(SHA.sha256(args_string))
    return joinpath(PACKAGE_DIR, "data", "serialized_system", "$hash_value")
end

function get_serialized_filepath(
    name::String,
    case_args::Dict{Symbol, <:Any} = Dict{Symbol, Any}(),
)
    dir = get_serialization_dir(case_args)
    return joinpath(dir, "$(name).json")
end

function is_serialized(name::String, case_args::Dict{Symbol, <:Any} = Dict{Symbol, Any}())
    file_path = get_serialized_filepath(name, case_args)
    return isfile(file_path)
end

function get_raw_data(; kwargs...)
    if haskey(kwargs, :raw_data)
        return kwargs[:raw_data]
    else
        throw(ArgumentError("Raw data directory not passed in build function."))
    end
end

function filter_kwargs(; kwargs...)
    system_kwargs = filter(x -> in(first(x), PSY.SYSTEM_KWARGS), kwargs)
    return system_kwargs
end

"""
Creates a JSON file informing the user about the meaning of the hash value in the file path
if it doesn't exist already 
"""
function serialize_case_parameters(case_args::Dict{Symbol, <:Any})
    dir_path = get_serialization_dir(case_args)
    file_path = joinpath(dir_path, "case_parameters.json")

    if !isfile(file_path)
        open(file_path, "w") do io
            JSON3.write(io, case_args)
        end
    end
end

function _get_system_directories(path::String)
    return (
        joinpath(path, x) for
        x in readdir(path) if isdir(joinpath(path, x)) && _is_system_hash_name(x)
    )
end

_is_system_hash_name(name::String) = isempty(filter(!isxdigit, name)) && length(name) == 64


function convert_to_hydropump!(d::EnergyReservoirStorage, sys::System)
    storage_capacity_MWh = d.storage_capacity * d.base_power
    reservoir_cost = HydroReservoirCost(;
        level_shortage_cost = d.operation_cost.energy_shortage_cost,
        level_surplus_cost = d.operation_cost.energy_surplus_cost,
        spillage_cost = 0.0,
    )
    head_reservoir = HydroReservoir(;
        name = "$(d.name)_head_reservoir",
        available = d.available,
        storage_level_limits = (
            min = storage_capacity_MWh * d.storage_level_limits.min,
            max = storage_capacity_MWh * d.storage_level_limits.max,
        ),
        initial_level = d.initial_storage_capacity_level,
        spillage_limits = nothing,
        inflow = 0.0,
        outflow = 0.0,
        level_targets = d.storage_target,
        travel_time = nothing,
        intake_elevation = 0.0,
        head_to_volume_factor = 0.0,
        operation_cost = reservoir_cost,
        level_data_type = ReservoirDataType.ENERGY,
    )
    tail_reservoir = HydroReservoir(nothing)
    PSY.set_name!(tail_reservoir, "$(d.name)_tail_reservoir")
    hpump = HydroPumpTurbine(;
        name = "$(d.name)_pump",
        available = d.available,
        bus = d.bus,
        active_power = d.active_power,
        reactive_power = d.reactive_power,
        rating = d.rating,
        active_power_limits = d.output_active_power_limits,
        reactive_power_limits = d.reactive_power_limits,
        active_power_limits_pump = d.input_active_power_limits,
        outflow_limits = nothing,
        head_reservoir = head_reservoir,
        tail_reservoir = tail_reservoir,
        powerhouse_elevation = 0.0,
        ramp_limits = nothing,
        time_limits = nothing,
        base_power = d.base_power,
        operation_cost = HydroGenerationCost(;
            variable = d.operation_cost.discharge_variable_cost,
            fixed = d.operation_cost.fixed,
        ),
        active_power_pump = 0.0,
        efficiency = (turbine = d.efficiency.out, pump = d.efficiency.in),
        prime_mover_type = d.prime_mover_type,
    )
    add_component!(sys, hpump)
    add_component!(sys, head_reservoir)
    add_component!(sys, tail_reservoir)
    for service in PSY.get_services(d)
        PSY.add_service!(hpump, service, sys)
    end
    copy_time_series!(hpump, d)
end
